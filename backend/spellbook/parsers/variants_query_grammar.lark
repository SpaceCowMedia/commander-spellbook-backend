%import common.WS
%ignore WS
%import common.INT -> NUMBER
%import common.NUMBER -> PRICE

start: query?

?query: term //| query OR term

?term: factor | term AND? factor

?factor: matcher | LPAREN query RPAREN

?matcher: CARD_SEARCH_SHORTCUT -> card_search_shortcut | pair

STRING: SHORT_LITERAL | LONG_LITERAL

STRING_OR_NUMBER: STRING | NUMBER

CARD_SEARCH_SHORTCUT: NEGATION_OPERATOR? STRING

?pair: CARD_SEARCH -> card_search
    | CARD_TYPE_SEARCH -> card_type_search
    | CARD_ORACLE_SEARCH -> card_oracle_search
    | CARD_KEYWORD_SEARCH -> card_keyword_search
    | CARD_MANA_VALUE_SEARCH -> card_mana_value_search
    | IDENTITY_SEARCH -> identity_search
    | PREREQUISITES_SEARCH -> prerequisites_search
    | STEPS_SEARCH -> steps_search
    | RESULTS_SEARCH -> results_search
    | SPELLBOOK_ID_SEARCH -> spellbook_id_search
    | TAG_SEARCH -> tag_search
    | COMMANDER_SEARCH -> commander_search
    | LEGALITY_SEARCH -> legality_search
    | PRICE_SEARCH -> price_search
    | POPULARITY_SEARCH -> popularity_search

CARD_SEARCH: NEGATION_OPERATOR? CARD_SEARCH_KEY COMPARISON_OPERATOR NUMBER
    | NEGATION_OPERATOR? CARD_SEARCH_KEY EQUALITY_OPERATOR STRING

CARD_TYPE_SEARCH: NEGATION_OPERATOR? CARD_TYPE_SEARCH_KEY EQUALITY_OPERATOR STRING

CARD_ORACLE_SEARCH: NEGATION_OPERATOR? CARD_ORACLE_SEARCH_KEY EQUALITY_OPERATOR STRING

CARD_KEYWORD_SEARCH: NEGATION_OPERATOR? CARD_KEYWORD_SEARCH_KEY MATCHES_OPERATOR STRING

CARD_MANA_VALUE_SEARCH: NEGATION_OPERATOR? CARD_MANA_VALUE_SEARCH_KEY COMPARISON_OPERATOR NUMBER

IDENTITY_SEARCH: NEGATION_OPERATOR? IDENTITY_SEARCH_KEY COMPARISON_OPERATOR STRING_OR_NUMBER

PREREQUISITES_SEARCH: NEGATION_OPERATOR? PREREQUISITES_SEARCH_KEY COMPARISON_OPERATOR NUMBER
    | NEGATION_OPERATOR? PREREQUISITES_SEARCH_KEY EQUALITY_OPERATOR STRING

STEPS_SEARCH: NEGATION_OPERATOR? STEPS_SEARCH_KEY COMPARISON_OPERATOR NUMBER
    | NEGATION_OPERATOR? STEPS_SEARCH_KEY EQUALITY_OPERATOR STRING

RESULTS_SEARCH: NEGATION_OPERATOR? RESULTS_SEARCH_KEY COMPARISON_OPERATOR NUMBER
    | NEGATION_OPERATOR? RESULTS_SEARCH_KEY EQUALITY_OPERATOR STRING

SPELLBOOK_ID_SEARCH: NEGATION_OPERATOR? SPELLBOOK_ID_SEARCH_KEY EQUALITY_OPERATOR STRING

TAG_SEARCH: NEGATION_OPERATOR? TAG_SEARCH_KEY MATCHES_OPERATOR SHORT_LITERAL

COMMANDER_SEARCH: NEGATION_OPERATOR? COMMANDER_SEARCH_KEY EQUALITY_OPERATOR STRING

LEGALITY_SEARCH: NEGATION_OPERATOR? LEGAL_SEARCH_KEY MATCHES_OPERATOR SHORT_LITERAL

PRICE_SEARCH: NEGATION_OPERATOR? PRICE_SEARCH_KEY COMPARISON_OPERATOR PRICE

POPULARITY_SEARCH: NEGATION_OPERATOR? POPULARITY_SEARCH_KEY COMPARISON_OPERATOR NUMBER

OR.5: "OR" | "||" | "|"

AND.5: "AND" | "&&" | "&"

LPAREN: "("

RPAREN: ")"

NEGATION_OPERATOR: "-"

LONG_LITERAL.10: /"(?:[^"\\]|\\\")+"/

SHORT_LITERAL.-1: /[a-zA-Z0-9\u00C0-\u00FF_\-',+!\.]+/

MATCHES_OPERATOR: ":"

EQUALITY_OPERATOR: "=" | MATCHES_OPERATOR

COMPARISON_OPERATOR: "<=" | ">=" | "<" | ">" |  EQUALITY_OPERATOR

CARD_SEARCH_KEY: "card"i | "cards"i

CARD_TYPE_SEARCH_KEY: "cardtype"i | "type"i | "types"i | "t"i

CARD_ORACLE_SEARCH_KEY: "cardoracle"i | "oracle"i | "o"i  | "text"i

CARD_KEYWORD_SEARCH_KEY: "cardkeywords"i | "cardkeyword"i | "keyword"i | "keywords"i | "kw"i

CARD_MANA_VALUE_SEARCH_KEY: "cardmanavalue"i | "manavalue"i | "mv"i | "cmc"i

IDENTITY_SEARCH_KEY: "coloridentity"i | "identity"i | "color"i | "colors"i | "id"i | "ids"i | "c"i | "ci"i

PREREQUISITES_SEARCH_KEY: "prerequisites"i | "prerequisite"i | "prereq"i | "prereqs"i | "pre"i

STEPS_SEARCH_KEY: "steps"i | "step"i | "description"i | "desc"i

RESULTS_SEARCH_KEY: "results"i | "result"i | "res"i

SPELLBOOK_ID_SEARCH_KEY: "spellbookid"i | "sid"i

TAG_SEARCH_KEY: "tag"i | "tags"i | "is"i

COMMANDER_SEARCH_KEY: "commander"i

LEGAL_SEARCH_KEY: "legal"i | "format"i | "banned"i

PRICE_SEARCH_KEY: "price"i | "usd"i | "eur"i | SUPPORTED_STORE

POPULARITY_SEARCH_KEY: "popularity"i | "pop"i | "deck"i | "decks"i
